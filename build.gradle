plugins {
    id 'java-library'
    id "maven-publish"
}


// Plugin config
ext.pluginNameUpper = "HamsterEcoHelper"
ext.pluginNameLower = ext.pluginNameUpper.toLowerCase()
ext.majorVersion = 9
ext.minorVersion = 0
ext.minecraftVersion = "1.18"
sourceCompatibility = 17
targetCompatibility = 17

// Suppiled by Jenkins
ext.buildNumber = System.env.BUILD_NUMBER == null ? "x"                : "$System.env.BUILD_NUMBER"
ext.mavenDirectory = System.env.MAVEN_DIR == null ? "$projectDir/repo" : "$System.env.MAVEN_DIR"
ext.jdDirectory = System.env.JAVADOCS_DIR == null ? null               : "$System.env.JAVADOCS_DIR"

// Version used for distribution. Different from maven repo
group = "cat.nyaa"
archivesBaseName = "${pluginNameUpper}-mc$minecraftVersion"
version = "$majorVersion.$minorVersion.$buildNumber".toString()

// Repositories and dependencies
repositories {
    mavenCentral()
    maven { name 'Spigot';      url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { name 'Sonatype';    url 'https://oss.sonatype.org/content/groups/public' }
    maven { name 'NyaaCat';     url 'https://ci.nyaacat.com/maven/' }
    maven { name 'EssentialsX'; url 'https://repo.essentialsx.net/releases/'}
    maven { name "papermc";     url 'https://papermc.io/repo/repository/maven-public/'}
    maven { name 'aikar';       url 'https://repo.aikar.co/content/groups/aikar/' }
    maven { name 'jitpack';     url 'https://jitpack.io' }
}

dependencies {
    implementation 'org.jetbrains:annotations:22.0.0'
    compileOnly "org.spigotmc:spigot-api:$minecraftVersion-R0.1-SNAPSHOT"

    // other nyaa plugins
    if (gradle.hasProperty("useLocalDependencies") && gradle.useLocalDependencies) {
        compileOnly project(":NyaaCore")
        compileOnly project(":LockettePro")
    } else {
        compileOnly('cat.nyaa:nyaacore:9.0-SNAPSHOT') { transitive = true }
        compileOnly('me.crafter.mc:lockettepro:2.10-SNAPSHOT') { transitive = false }
    }

    // 3rd party plugins
    compileOnly ('com.github.MilkBowl:VaultAPI:1.7')
    compileOnly ('net.essentialsx:EssentialsX:2.19.0')
}

// source file modification (modify version string)
processResources {
    filesMatching("**/plugin.yml") {
        expand 'version': project.version
    }
}

// source file jar
task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

// javadoc generation options
javadoc {
    // javadoc output folder
    if (project.jdDirectory != null) destinationDir = file("${jdDirectory}/${pluginNameLower}-${version}")

    (options as StandardJavadocDocletOptions).with {
        links 'https://docs.oracle.com/en/java/javase/16/docs/api/'
        links 'https://hub.spigotmc.org/javadocs/spigot/'
        links 'https://google.github.io/guava/releases/21.0/api/docs/'
        links 'https://ci.md-5.net/job/BungeeCord/ws/chat/target/apidocs/'

        locale 'en_US'
        encoding 'UTF-8'
        docEncoding 'UTF-8'
        addBooleanOption('keywords', true)
        addStringOption('Xdoclint:none', '-quiet')

        addBooleanOption('html5', true)

        windowTitle = "${pluginNameUpper} Javadoc"
        docTitle = "${pluginNameUpper} (mc$minecraftVersion-${project.version})"
    }
}

// javadoc jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

// compile options
compileJava {
    options.compilerArgs += ["-Xlint:deprecation"]
}

// maven publish
publishing {
    publications {
        mavenJava(MavenPublication) {
            group      project.group
            artifactId pluginNameLower
            version    "$majorVersion.$minorVersion-SNAPSHOT"

            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
    repositories {
        maven {
            url mavenDirectory
        }
    }
}

